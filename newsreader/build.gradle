apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23"

    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(file('../version.properties')))

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 23
        versionName versionProps['name']
        versionCode versionProps['code'].toInteger()
        project.ext.set("archivesBaseName", "" + defaultConfig.versionName);
    }

    signingConfigs {
        def Properties localProps = new Properties()
        def Properties keyProps = new Properties()
        localProps.load(new FileInputStream(file('./local.properties')))
        assert localProps['keystore.props.file'];
        keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))

        release {
            storeFile file(keyProps["store"])
            keyAlias keyProps["alias.diariolibre"]
            storePassword keyProps["storePass"]
            keyPassword keyProps["pass"]
        }
    }


    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-rules.txt')
            debuggable false
            minifyEnabled true
            shrinkResources true
        }

        debug {
            debuggable true
            ext.enableCrashlytics = false
        }

        publicBeta.initWith(buildTypes.release)
        publicBeta {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-rules.txt')
            versionNameSuffix " " + versionProps['betaNumber']
        }

        publicDebug.initWith(buildTypes.publicBeta)
        publicDebug {
            debuggable true
            versionNameSuffix " Debug " + versionProps['betaNumber']
        }
    }


}

dependencies {
    compile 'com.android.support:support-v4:23.0.0'
    compile 'com.android.support:design:23.0.0'
    compile 'com.android.support:appcompat-v7:23.0.0'
    compile 'com.android.support:cardview-v7:23.0.0'
    compile 'com.android.support:recyclerview-v7:23.0.0'
    compile 'com.android.support:palette-v7:23.0.0'
    compile 'com.google.android.gms:play-services:7.8.0'
    compile('de.greenrobot:eventbus:2.2.1') {
        exclude group: 'com.google.android', module: 'support-v4' // already included below
    }

    compile 'com.github.bumptech.glide:glide:3.6.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.4.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'it.subito:masaccio-library:1.0.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.2.1@aar') {
        transitive = true;
    }
    compile fileTree(dir: 'libs', include: ['*.jar'])

}
