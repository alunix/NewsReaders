apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(file('../version.properties')))

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 21
        versionName versionProps['name']
        versionCode versionProps['code'].toInteger()
    }

    signingConfigs {
        def Properties localProps = new Properties()
        localProps.load(new FileInputStream(file('./local.properties')))
        def Properties keyProps = new Properties()
        assert localProps['keystore.props.file'];
        keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))

        diariolibre {
            storeFile file(keyProps["store"])
            keyAlias keyProps["alias.diariolibre"]
            storePassword keyProps["storePass"]
            keyPassword keyProps["pass"]
        }
        listindiario {
            storeFile file(keyProps["store"])
            keyAlias keyProps["alias.listindiario"]
            storePassword keyProps["storePass"]
            keyPassword keyProps["pass"]
        }
        elcaribe {
            storeFile file(keyProps["store"])
            keyAlias keyProps["alias.elcaribe"]
            storePassword keyProps["storePass"]
            keyPassword keyProps["pass"]
        }
        noticiassin {
            storeFile file(keyProps["store"])
            keyAlias keyProps["alias.noticiassin"]
            storePassword keyProps["storePass"]
            keyPassword keyProps["pass"]
        }
        elnacional {
            storeFile file(keyProps["store"])
            keyAlias keyProps["alias.elnacional"]
            storePassword keyProps["storePass"]
            keyPassword keyProps["pass"]
        }
    }


    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-rules.txt')
            debuggable false
            minifyEnabled true
            shrinkResources true
        }

        debug {
            debuggable true
        }

        publicBeta.initWith(buildTypes.release)
        publicBeta {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-rules.txt')
            versionNameSuffix " " + versionProps['betaNumber']
        }

        publicDebug.initWith(buildTypes.publicBeta)
        publicDebug {
            debuggable true
            versionNameSuffix " Debug " + versionProps['betaNumber']
        }
    }

    productFlavors {
        def diariolibreSigningConfig = signingConfigs.diariolibre
        def listindiarioSigningConfig = signingConfigs.listindiario
        def elcaribeSigningConfig = signingConfigs.elcaribe

        diariolibre {
            applicationId "com.wmendez.diariolibre"
            manifestPlaceholders = [
                    appName: ".App",
                    mainActivity:"com.wmendez.diariolibre.MainActivity"
            ]
            signingConfig diariolibreSigningConfig

        }

        elcaribe {
            applicationId "com.wmendez.elcaribe"
            manifestPlaceholders = [
                    appName: ".App",
                    mainActivity:"com.wmendez.elcaribe.MainActivity"
            ]
            signingConfig elcaribeSigningConfig
        }

        listindiario {
            applicationId "com.wmendez.listindiario"
            manifestPlaceholders = [
                    appName: ".App",
                    mainActivity:"com.wmendez.listindiario.MainActivity"
            ]
            signingConfig listindiarioSigningConfig
        }

    }
}


dependencies {
    compile 'com.android.support:support-v4:21.0.3'
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.android.support:cardview-v7:21.0.0'
    compile 'com.android.support:recyclerview-v7:21.0.0'
    compile 'com.android.support:palette-v7:21.0.0'
    compile 'org.jsoup:jsoup:1.7.3'
    compile('de.greenrobot:eventbus:2.2.1') {
        exclude group: 'com.google.android', module: 'support-v4' // already included below
    }
    compile 'com.squareup.picasso:picasso:2.4.0'
    compile 'com.squareup.okhttp:okhttp:2.1.0-RC1'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.1.0-RC1'
    compile 'com.google.android.gms:play-services:6.5.87'
    compile 'com.squareup.retrofit:retrofit:1.8.0'
    compile 'com.jakewharton:butterknife:5.1.2'
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
